plugins {
  id "jacoco"
  id "idea"
  id "java"
  id 'org.sonarqube' version '6.1.0.5360'
  id "com.diffplug.spotless" version "7.0.3"
  id "com.google.cloud.tools.jib" version "3.4.5"
  id 'org.cyclonedx.bom' version '2.2.0'
}

cyclonedxBom {
  schemaVersion = "1.5"
}

repositories {
  mavenCentral()
}

group "com.sdase.k8s.operator.mongodb"

idea.module.inheritOutputDirs = true

version = System.getenv('SEMANTIC_VERSION')

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
    vendor = JvmVendorSpec.ADOPTIUM
  }
}
tasks.withType(JavaCompile).configureEach {
  options.encoding = 'UTF-8'
}

jib {
  container.ports = ['8081']
  container.args = []
  from.image = 'gcr.io/distroless/java21-debian12:nonroot'
  container.user = 10001
  to.image = "$rootProject.name:latest"
  container.labels = [
      'org.opencontainers.image.title': rootProject.name,
      'org.opencontainers.image.version': version,
      'org.opencontainers.image.url': "https://github.com/SDA-SE/${rootProject.name}",
      'org.opencontainers.image.source': "https://github.com/SDA-SE/${rootProject.name}",
      'org.opencontainers.image.licenses': "MIT",
      'org.opencontainers.image.description': "A Kubernetes Operator that creates users and databases in existing MongoDB clusters based on a custom resource.",
  ]
}

project.ext {
  mongoDbDriverVersion = "5.4.0"
  slf4jVersion = "2.0.17"
  operatorFrameworkVersion = "5.0.4"
  kubernetesServerMockVersion = "7.1.0" // align with transitive dependency of operator framework
  mockitoVersion = "5.2.0"
  jacksonVersion = "2.18.3"
  logbackContribVersion = "0.1.5"
  nettyVersion = "4.2.0.Final"
}

dependencies {

  implementation enforcedPlatform("io.netty:netty-bom:${nettyVersion}")
  annotationProcessor enforcedPlatform("io.netty:netty-bom:${nettyVersion}")
  implementation enforcedPlatform("com.fasterxml.jackson:jackson-bom:${jacksonVersion}")
  annotationProcessor enforcedPlatform("com.fasterxml.jackson:jackson-bom:${jacksonVersion}")
  implementation enforcedPlatform('org.eclipse.jetty:jetty-bom:11.0.25')
  implementation enforcedPlatform('org.jetbrains.kotlin:kotlin-bom:2.1.20')
  annotationProcessor enforcedPlatform("org.slf4j:slf4j-bom:${slf4jVersion}")
  implementation enforcedPlatform("org.slf4j:slf4j-bom:${slf4jVersion}")

  implementation "io.javaoperatorsdk:operator-framework:${operatorFrameworkVersion}", {
    // recheck when operator-framework is upgraded
    exclude group: 'com.squareup.okio', module: 'okio'
  }
  annotationProcessor "io.javaoperatorsdk:operator-framework:${operatorFrameworkVersion}", {
    // recheck when operator-framework is upgraded
    exclude group: 'com.squareup.okio', module: 'okio'
  }
  // upgrade from transitive 1.15.0 in okhttp to fix CVE-2023-3635
  implementation 'com.squareup.okio:okio:3.11.0'

  implementation "org.mongodb:mongodb-driver-sync:${mongoDbDriverVersion}"

  implementation 'org.hibernate.validator:hibernate-validator:8.0.2.Final'
  implementation 'jakarta.el:jakarta.el-api:6.0.1'
  implementation 'org.bouncycastle:bcpkix-jdk18on:1.80'
  implementation "io.javalin:javalin:6.5.0"

  // logging
  implementation "org.slf4j:slf4j-api"
  implementation 'ch.qos.logback:logback-classic:1.5.18'
  implementation "ch.qos.logback.contrib:logback-json-classic:${logbackContribVersion}", {
    exclude group: "ch.qos.logback", module: "logback-classic"
    exclude group: "ch.qos.logback", module: "logback-core"
  }
  implementation "ch.qos.logback.contrib:logback-jackson:${logbackContribVersion}", {
    exclude group: "ch.qos.logback", module: "logback-core"
  }

  implementation 'io.micrometer:micrometer-registry-prometheus:1.14.5'

  // test
  testImplementation enforcedPlatform("org.junit:junit-bom:5.12.1")
  testImplementation 'org.junit.jupiter:junit-jupiter'
  testImplementation 'org.junit-pioneer:junit-pioneer:2.3.0'
  testImplementation 'org.assertj:assertj-core:3.27.3', {
    // conflict of 1.12.10 in assertj vs 1.12.13 in mockito-core
    exclude group: "net.bytebuddy", module: "byte-buddy"
    exclude group: "net.bytebuddy", module: "byte-buddy-agent"
  }
  // try to replace local commons-compress management on update!
  testImplementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo:4.20.0', {
    // newer version from operator-framework
    exclude group: "org.apache.commons", module: "commons-lang3"
  }
  testImplementation "org.mongodb:mongodb-driver-legacy:${mongoDbDriverVersion}"
  testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}", {
    // conflict of 1.14.1 and 1.14.7 in uk.org.webcompere:system-stubs-jupiter
    exclude group: 'net.bytebuddy', 'module': 'byte-buddy'
    exclude group: 'net.bytebuddy', 'module': 'byte-buddy-agent'
  }
  testImplementation "uk.org.webcompere:system-stubs-jupiter:2.1.8"
  testImplementation "org.mockito:mockito-inline:${mockitoVersion}", {
    // conflict of 1.14.1 and 1.14.7 in uk.org.webcompere:system-stubs-jupiter
    exclude group: 'net.bytebuddy', 'module': 'byte-buddy'
    exclude group: 'net.bytebuddy', 'module': 'byte-buddy-agent'
  }
  testImplementation 'org.awaitility:awaitility:4.3.0'
  testImplementation "io.fabric8:kubernetes-server-mock:${kubernetesServerMockVersion}", {
    exclude group: 'com.squareup.okio', module: 'okio'
    exclude group: 'junit', module: 'junit'
  }
  // CVE-2020-15250 in 4.12
  // -> pulled transitively from OkHttp3 mockwebserver used by kubernetes-server-mock
  testImplementation 'junit:junit:4.13.2'
  testImplementation 'com.squareup.okhttp3:okhttp:4.12.0', {
    exclude group: 'com.squareup.okio', module: 'okio'
  }
}

dependencyLocking { lockAllConfigurations() }

// Task for creating gradle.lockfile per module. Needed for Trivy vulnerability scan.
tasks.register('resolveAndLockAll') {
  doFirst {
    assert gradle.startParameter.writeDependencyLocks
  }
  doLast {
    configurations.findAll {
      it.canBeResolved
    }.each { it.resolve() }
  }
}

test {
  useJUnitPlatform()
}

spotless {
  java { googleJavaFormat('1.18.1') }
  enforceCheck = false
}

configurations.configureEach {
  if (it.name.startsWith("spotless")) {
    // Workaround for https://github.com/diffplug/spotless/issues/1965.
  } else {
    it.resolutionStrategy {
      failOnVersionConflict()
    }
  }
}

// begin config for SonarCloud
jacocoTestReport {
  reports {
    xml.required = true
  }
}

sonar {
  properties {
    property 'sonar.projectKey', 'SDA-SE_mongodb-operator'
    property 'sonar.organization', 'sda-se'
    property 'sonar.host.url', 'https://sonarcloud.io'
    property 'sonar.token', System.getenv('SONAR_LOGIN_TOKEN')

    property 'sonar.pullrequest.provider', 'GitHub'
    property 'sonar.pullrequest.github.repository', System.getenv('GITHUB_REPOSITORY')

    if (System.getenv('GITHUB_PR') != null && System.getenv('GITHUB_PR') != "") {
      property 'sonar.pullrequest.key', System.getenv('GITHUB_PR')
      if (System.getenv('GITHUB_BRANCH') != null) {
        property 'sonar.pullrequest.branch', System.getenv('GITHUB_BRANCH')
      }
    }

    if (System.getenv('SONAR_SCM_REVISION') != null) {
      property 'sonar.scm.revision', System.getenv('SONAR_SCM_REVISION')
    }

  }
}
// end config for SonarCloud
